<Goal> - <Funcs>
<Funcs> - <Func> <Funcs>
<Funcs> - @
<Func> - <FuncType> [Identifier] ( <Args> ) { <Statements> }
<Func> - void main ( ) { <Statements> }
<FuncType> - <Type>
<FuncType> - void
<Type> - int <Arr>
<Type> - bool <Arr>
<Type> - char <Arr>
<Type> - string <Arr>
<Type> - float <Arr>
<Arr> - [ <IntExpression> ] <Arr>
<Arr> - @
<Args> - <Type> [Identifier] <CommaArgs>
<Args> - @
<CommaArgs> - , <Type> [Identifier] <CommaArgs>
<CommaArgs> - @
<Statements> - <Statement> <Statements>
<Statements> - @
<Statement> - { <Statements> }
<Statement> - if ( <BoolExpression> ) <Statement> <Else>
<Statement> - while ( <BoolExpression> ) <Statement>
<Statement> - print ( <CharExpression> ) ;
<Statement> - read ( ) ;
<Statement> - [Identifier] ( <Expressions> ) ;
<Statement> - <Type> [Identifier] ;
<Statement> - <Type> [Identifier] = <Expression> ;
<Statement> - [Identifier] <Arr> = <Expression> ;
<Statement> - return <Expression> ;
<Statement> - return ;
<Else> - else <Statement>
<Else> - @
<Expression> - <VarExpression>
<Expression> - [Char]
<Expression> - read ( )
<Expression> - [String] . <StringExpression>
<Expression> - <VarExpression> . <StringExpression>
<Expression> - [String]
<Expression> - <FloatWithoutVar> <MulDiv> <FloatProductQuotient> <PlusMinus> <FloatExpression>
<Expression> - <VarExpression> <MulDiv> <FloatProductQuotient> <PlusMinus> <FloatExpression>
<Expression> - <FloatWithoutVar> <PlusMinus> <FloatExpression>
<Expression> - <VarExpression> <PlusMinus> <FloatExpression>
<Expression> - <FloatWithoutVar> <MulDiv> <FloatProductQuotient>
<Expression> - <VarExpression> <MulDiv> <FloatProductQuotient>
<Expression> - <FloatWithoutVar>
<Expression> - <BoolWithoutVar> && <AndResult> || <BoolExpression>
<Expression> - <VarExpression> && <AndResult> || <BoolExpression>
<Expression> - <BoolWithoutVar> || <BoolExpression>
<Expression> - <VarExpression> || <BoolExpression>
<Expression> - <BoolWithoutVar> && <AndResult>
<Expression> - <VarExpression> && <AndResult>
<Expression> - <BoolWithoutVar>
<Expressions> - <Expression> <CommaExpressions>
<Expressions> - @
<CommaExpressions> - , <Expression> <CommaExpressions>
<CommaExpressions> - @
<FloatWithoutVar> - float ( <FloatExpression> )
<FloatWithoutVar> - int ( <FloatExpression> )
<FloatWithoutVar> - [FloatPointNumber]
<FloatWithoutVar> - [Integer]
<FloatWithoutVar> - [Binary]
<FloatWithoutVar> - [Hexadecimal]
<FloatWithoutVar> - - <Float>
<BoolWithoutVar> - bool ( <BoolExpression> )
<BoolWithoutVar> - true
<BoolWithoutVar> - false
<BoolWithoutVar> - ! <Bool>
<BoolWithoutVar> - <Comparison> ( <Expression> , <Expression> )
<VarExpression> - [Identifier] ( <Expressions> ) <Arr>
<VarExpression> - [Identifier] <Arr>
<CharExpression> - <VarExpression>
<CharExpression> - [Char]
<CharExpression> - read ( )
<StringExpression> - [String] . <StringExpression>
<StringExpression> - <VarExpression> . <StringExpression>
<StringExpression> - [String]
<StringExpression> - <VarExpression>
<IntExpression> - <IntProductQuotient> <PlusMinus> <IntExpression>
<IntExpression> - <IntProductQuotient>
<IntProductQuotient> - <Int> <MulDiv> <IntProductQuotient>
<IntProductQuotient> - <Int>
<Int> - <VarExpression>
<Int> - int ( <IntExpression> )
<Int> - [Integer]
<Int> - [Binary]
<Int> - [Hexadecimal]
<Int> - - <Int>
<FloatExpression> - <FloatProductQuotient> <PlusMinus> <FloatExpression>
<FloatExpression> - <FloatProductQuotient>
<FloatProductQuotient> - <Float> <MulDiv> <FloatProductQuotient>
<FloatProductQuotient> - <Float>
<Float> - <VarExpression>
<Float> - float ( <FloatExpression> )
<Float> - int ( <FloatExpression> )
<Float> - [FloatPointNumber]
<Float> - [Integer]
<Float> - [Binary]
<Float> - [Hexadecimal]
<Float> - - <Float>
<BoolExpression> - <AndResult> || <BoolExpression>
<BoolExpression> - <AndResult>
<AndResult> - <Bool> && <AndResult>
<AndResult> - <Bool>
<Bool> - <VarExpression>
<Bool> - bool ( <BoolExpression> )
<Bool> - true
<Bool> - false
<Bool> - ! <Bool>
<Bool> - <Comparison> ( <Expression> , <Expression> )
<PlusMinus> - +
<PlusMinus> - -
<MulDiv> - *
<MulDiv> - /
<MulDiv> - %
<Comparison> - equal
<Comparison> - notEqual
<Comparison> - less
<Comparison> - greater
<Comparison> - lessEqual
<Comparison> - greaterEqual
